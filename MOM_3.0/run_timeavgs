#! /bin/csh -f
echo " "
echo " =>executing: run_timeavgs"
echo " "
#
#    script for executing the "timeavgs.F" module
#
#     options: (must choose only one of these)
#       -Dread_my_grid    : read in your own grid definition
#       -Dgenerate_a_grid : generate a grid based on USER INPUT
#                           in grids.F

set OPTIONS = '-Ddrive_timeavgs -Dgenerate_a_grid'

set MOMpath = /net/rcp/MOM_3        # base model code
set WORK = TEMP_DIR                 # temp directory
set OUTPUT = `pwd`                  # directory holding run script
                                    # and output files

set error = false
if (! -r timeavgs.F) then
  echo '=>Error: "timeavgs.F" is not in this UPDATES directory'
  set error = true
endif

if (! -r grids.F) then
  echo '=>Error: "grids.F" is not in this UPDATES directory'
  set error = true
endif

if (! -r size.h) then
  echo '=>Error: "size.h" is not in this UPDATES directory'
  set error = true
endif

if ("$error" == "true") then
  exit
endif

echo "=>Grabbing sources from $MOMpath"
mkdir $WORK
set list = (stdunits.h derived_options.h util.F iomngr.F iomngr.h size_check.F coord.h)
foreach file ($list)
  cp $MOMpath/$file $WORK
end
cp timeavgs.F $WORK
cp grids.F $WORK
cp size.h $WORK
cd $WORK

echo "=>compiling..."
f90 $OPTIONS timeavgs.F

echo "=> running...results are being written to results_timeavgs"
a.out > $OUTPUT/results_timeavgs

cd $OUTPUT
/bin/rm -r $WORK

echo " "
tail results_timeavgs

echo " "
echo "=> Done"
    
