#! /bin/csh -f
#   run script for MOM_3 test cases on CRAY T90 at GFDL
#   author:  r.c.pacanowski   e-mail: rcp@gfdl.gov
#
# ======================================================================
#  set all paths (these must be changed if running outside of GFDL)
# ======================================================================

set myEXP        = TEST_CASE
set myHOST       = rcp: 
set datainpath   = /archive/rcp/MOM_3/EXP/$myEXP
set DATABASE     = /archive/rcp/MOM_3/DATABASE
set MOMpath      = /net/rcp/MOM_3
set MOMupdates   = $MOMpath/EXP/$myEXP/MOM_UPDATES
set printoutpath = $MOMpath/EXP/$myEXP 
set WORKDIR      = $FTMPDIR
set NETCDFlib    = "-L/usr/local/lib -ludunits -L/usr/local/lib -lnetcdf"

# ======================================================================
# select a case. start with CASE = '0' (uses internally generated data)
# ======================================================================

set CASE = '0' # idealized zonal average surface boundary conditions
#set CASE = '1' # TIME_MEAN ...constant surface boundary conditions
#                              requires data prepared in PREP_DATA
#set CASE = '2' # MONTHLY   ...monthly mean surface boundary conditions 
#                              requires data prepared in PREP_DATA
#set CASE = '3' # ATMOS ... simple atmos model to illustrate coupling
#                           uses internally generated data
# ======================================================================
# select real data (needs to have been prepared using PREP_DATA)
# or idealized data (uses internally generated data)  
# ======================================================================

set LEVITUS_DATA_IC = no # uses idealized T & S as initial condition
#set LEVITUS_DATA_IC = yes # uses jan levitus T & S as initial condition
set SPONGE_DATA     = no # no sponge boundary
#set SPONGE_DATA     = yes # uses sponges for limited domain models
set SCRIPPS_DATA    = no # internally generated idealized topography
#set SCRIPPS_DATA    = yes # uses scripps based topography

# ======================================================================
# first, compile everything. after that, if changes are made to only a
# few subroutines, then just compile the ones that have changed.
# if "ifdef options" or a .h file is changed, then recompile everything.
# ======================================================================

set COMPILE_LIST = "all"         # "all" compiles everything

#set COMPILE_LIST = "  setocn.F" # will compile just these

set  NUM_PROCESSORS = 1 #unitasking
#set NUM_PROCESSORS = 2 #multitasking (any number >1)

set BOUNDS_CHECKER = no
#set BOUNDS_CHECKER = yes

# ======================================================================
# set the appropriate atmosphere path to supply surface b.c.
# ======================================================================

if ($CASE == '0') then
 echo "==> Configuring for internally generated S.B.C. TEST CASE #$CASE"
else if ($CASE == '1') then
 set ATMOSpath = $MOMpath/SBC/TIME_MEAN
 echo "==> Configuring for TIME_MEAN S.B.C. TEST CASE #$CASE"
else if ($CASE == '2') then
 set ATMOSpath = $MOMpath/SBC/MONTHLY
 echo "==> Configuring for MONTHLY S.B.C. TEST CASE #$CASE"
else if ($CASE == '3') then
 set ATMOSpath = $MOMpath/SBC/ATMOS
 echo "==> Configuring for ATMOS S.B.C. TEST CASE #$CASE"
endif

# ======================================================================
#   define i/o control  for writing diagnostics.
#   if < 0 then output is written to unformatted file and stdout
#   if > 0 then output is written to unformatted file only
#   if = stdout = 6 then output is written to stdout only
#   this applies only to those diagnostics which are "enabled"
# ======================================================================

set iotraj   = -1 # particle trajectories
set iotavg   = -1 # regional tracer averages
set iotmb    = -1 # tracer meridional balance diagnostics
set ioglen   = -1 # global energetic diagnostics
set iotrmb   = -1 # term balance diagnostic
set iovmsf   = -1 # meridional mass transport diagnostic
set iogyre   = -1 # gyre transport diagnostic
set ioprxz   = -1 # zonal x-z matrix diagnostics
set ioext    = -1 # show external mode (stream function) diagnostic
set iodsp    = -1 # diagnostic surface pressure diagnostics
set iotsi    = -1 # time step monitor integral diagnostics
set iozmbc   = -1 # zonal mean S.B.C. diagnostics
set ioxbt    = -1 # XBT stations


# ======================================================================
# enable options
# ======================================================================
echo " "; echo "OPTIONS follow:" ;echo " "


set OPTS = " "
if ($LEVITUS_DATA_IC == 'yes') then
 set OPTS = " $OPTS -Dlevitus_ic"
else
 set OPTS = " $OPTS -Didealized_ic"
endif

if ($SCRIPPS_DATA == 'yes') then
 set OPTS = " $OPTS -Dscripps_kmt"
else
 set OPTS = " $OPTS -Didealized_kmt"
endif

if ($SPONGE_DATA == 'yes') then
 set OPTS = " $OPTS -Dsponges"
endif

if ($CASE == '0') then
 set OPTS = "$OPTS -Dsimple_sbc"
else if ($CASE == '1') then
 set OPTS = "$OPTS -Dtime_mean_sbc_data"
else if ($CASE == '2') then
 set OPTS = "$OPTS -Dtime_varying_sbc_data"
else if ($CASE == '3') then
 set OPTS = "$OPTS -Dcoupled -Dtrace_coupled_fluxes"
endif

#set TIMING_OPTIONS = "-Dtiming "
set TIMING_OPTIONS = " "
echo TIMING_OPTIONS = $TIMING_OPTIONS; echo " "


set PLATFORM_OPTION = "-Dcray_t90"
#set PLATFORM_OPTION = "-Dsgi"
if ("$PLATFORM_OPTION" == "-Dcray_t90") then  # needed for NETCDF routines
  set PLATFORM_OPTION = ($PLATFORM_OPTION -DHOST_T90) 
else if ("$PLATFORM_OPTION" == "-Dcray_c90") then
  set PLATFORM_OPTION = ($PLATFORM_OPTION -DHOST_T90) # not HOST_C90
else if ("$PLATFORM_OPTION" == "-Dcray_ymp") then
  set PLATFORM_OPTION = ($PLATFORM_OPTION -DHOST_T90) # not HOST_YMP
else if ("$PLATFORM_OPTION" == "-Dsgi") then
  set PLATFORM_OPTION = ($PLATFORM_OPTION -DHOST_SGI)
endif
echo PLATFORM_OPTION = $PLATFORM_OPTION
echo " "

set IO_OPTIONS = "-Dramdrive"
#set IO_OPTIONS = "-Dcrayio"
#set IO_OPTIONS = "-Dfio"
echo IO_OPTIONS = $IO_OPTIONS; echo " "


set GRID_OPTIONS = "-Dgenerate_a_grid"
echo GRID_OPTIONS = $GRID_OPTIONS; echo " "


set TOPOGRAPHY_OPTIONS = "-Dcyclic -Dfill_perimeter_violations -Dfill_isolated_cells"
echo TOPOGRAPHY_OPTIONS = $TOPOGRAPHY_OPTIONS; echo " "


#set EXMODE_OPTIONS = "-Dstream_function -Dsf_9_point "
#set EXMODE_OPTIONS = "-Dstream_function -Dsf_5_point "
#set EXMODE_OPTIONS = "-Drigid_lid_surface_pressure -Dsf_9_point "
set EXMODE_OPTIONS = "-Dimplicit_free_surface -Dsf_9_point "
#set EXMODE_OPTIONS = "-Dexplicit_free_surface -Dexplicit_eb -Dexplicit_dpdc "
echo EXMODE_OPTIONS = $EXMODE_OPTIONS; echo " "

set DEBUG_OPTIONS = " "
echo DEBUG_OPTIONS = $DEBUG_OPTIONS; echo " "


set COMPILER_OPTIONS = " -O 2 "
#set COMPILER_OPTIONS = " -O 0 "

if ($BOUNDS_CHECKER == "yes") then
 set COMPILER_OPTIONS = ( $COMPILER_OPTIONS -R b )
endif

source /local/f90/2.0.4.0 #latest compiler
if ($NUM_PROCESSORS != 1) then
 set COMPILER_OPTIONS = ( -ataskcommon $COMPILER_OPTIONS )
 set PARALLEL_OPTIONS = " -Dparallel_1d "
 module load mpt         #message passing toolkit library
else
 set PARALLEL_OPTIONS = " "
endif

echo "=>COMPILER OPTIONS = $COMPILER_OPTIONS"; echo " "

echo "=>PARALLEL OPTIONS = $PARALLEL_OPTIONS"; echo " "

#set OPT2 = " -Dfirfil -Drestorst -Dconstvmix -Dconsthmix " 
set OPT2 = " -Dfourfil -Drestorst -Dconstvmix -Dconsthmix " 
#set OPT2 = " -Drestorst -Dconstvmix -Dconsthmix " 
#set OPT2 = " -Dfirfil -Drestorst -Dconstvmix -Dconsthmix -Dbiharmonic  " 
#set OPT2 = " -Dfirfil -Drestorst -Dppvmix -Dconsthmix -Dbiharmonic  " 
#set OPT2 = " -Dfirfil -Drestorst -Dconsthmix -Dconstvmix -Dredi_diffusion "
echo OPT2 = $OPT2; echo " "

#

set OPT = "$TIMING_OPTIONS $PLATFORM_OPTION $IO_OPTIONS $GRID_OPTIONS $TOPOGRAPHY_OPTIONS $EXMODE_OPTIONS $DEBUG_OPTIONS $PARALLEL_OPTIONS"
echo OPT = $OPT ; echo " "          
           
if ($NUM_PROCESSORS != 1) then
 set DIAGNOSTICS = (-Dtime_step_monitor -Denergy_analysis)
else           
# set DIAGNOSTICS = (-Dtime_step_monitor -Denergy_analysis -Dxbts -Dterm_balances \
#                   -Dgyre_components -Dmeridional_overturning -Dtracer_averages \
#		   -Dshow_zonal_mean_of_sbc -Dshow_external_mode -Ddiagnostic_surf_height \
#		   -Dmeridional_tracer_budget -Dtrajectories -Dstability_tests \
#		   -Dmatrix_sections -Dsnapshots -Dtime_averages \
#                   -Dsave_convection -Dsave_mixing_coeff )
 set DIAGNOSTICS = (-Dtime_step_monitor -Dsnapshots)
endif
echo DIAGNOSTICS = $DIAGNOSTICS; echo " "

#set NETCDF = "" # no diagnostic output is netcdf format
set NETCDF = "-Dnetcdf"  # make all diagnostic output in netcdf format
#set NETCDF = "-Dtracer_yz" # only netcdf format (no IEEE)
#set NETCDF = "-Ddensity_netcdf -Dcross_flow_netcdf" # only netcdf format (no IEEE)

if ("$NETCDF" == "") then #no NETCDF library
 set NETCDFlib = ""
endif

set OPTIONS = "$OPTS  $OPT2 $OPT $NETCDF"

echo OPTIONS = $OPTIONS; echo " "

# ======================================================================
# change to a working directory and clean it out (except restart file)
# ======================================================================

echo "==> changing to a working directory and cleaning it out"
cd $WORKDIR
if ("$COMPILE_LIST" == "all") then
  /bin/rm $WORKDIR/*.[Ff]
  /bin/rm $WORKDIR/*.F90
  /bin/rm $WORKDIR/*.inc
endif
if (-r restart.dta) then
 /bin/mv restart.dta restart.dta.save
 /bin/rm $WORKDIR/*.dta
 /bin/mv restart.dta.save restart.dta
else
 /bin/rm $WORKDIR/*.dta
endif
/bin/rm $WORKDIR/*.nc

# ======================================================================
# copy all code to the working directory
# ======================================================================

if ("$COMPILE_LIST" == "all") then
 if ("$NETCDF" != "") then
  echo "==> copying NETCDF files from $MOMpath/NETCDF "
  rcp "$myHOST$MOMpath/NETCDF/*" .
  if (-r /usr/local/include/netcdf.inc) then
   /bin/cp /usr/local/include/netcdf.inc .
  else
   echo "Error: file /usr/local/include/netcdf.inc is missing."
   exit
  endif
 endif

 echo "==> copying files from $MOMpath"
 rcp "$myHOST$MOMpath/*.F90" . 
 rcp "$myHOST$MOMpath/*.[Ffh]" .
 rcp "$myHOST$MOMpath/*.inc" .
endif

if ($CASE != '0') then
 echo "==> copying  ATMOS files from $ATMOSpath"
 rcp "$myHOST$ATMOSpath/*.[Ffh]" . 
 rcp "$myHOST$ATMOSpath/*.F90" . 
endif

echo "==> copying updated files from $MOMupdates"
rcp "$myHOST$MOMupdates/*.[Ffh]" . 
rcp "$myHOST$MOMupdates/*.F90" . 
rcp "$myHOST$MOMupdates/*.inc" . 


# ======================================================================
# remove executable
# ======================================================================

/bin/rm a.out

# ======================================================================
# compile and link 
# ======================================================================

setenv NPROC 5


if ($BOUNDS_CHECKER == "yes") then
  echo "=> compiling with the bounds checker option"
endif

if ("$COMPILE_LIST" == "all") then
  set list = `ls  *.[Ff] `
  /bin/rm *.o
else
  set list = ($COMPILE_LIST)
endif

echo " ";echo "=> Preprocessing and Compiling the following..."
set list90 = `ls *.F90`
foreach file ($list90)
 echo $file
 f90 $COMPILER_OPTIONS $DIAGNOSTICS $OPTIONS -c $file
end
foreach file ($list)
 echo $file
 f90 $COMPILER_OPTIONS $DIAGNOSTICS $OPTIONS -c $file
end
echo "=> linking ..."
f90  -Wl"-S 250000 -D preset=nan $NETCDFlib" *.o
echo "           ... done linking"


# ======================================================================
#   set namelist variables
# ======================================================================

set days = 4.0
set diag = 4.0
set days = 20.0
set diag = 10.0
 
  set NAMELIST_EOF = "/"

cat > namelist.procs << ENDNAMELIST
 &procs  num_processors = $NUM_PROCESSORS 
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.contrl << ENDNAMELIST
 &contrl init=.true., runlen=$days, rununits='days',
         restrt=.false., initpt=.true.
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.mbcin << ENDNAMELIST
 &mbcin   $NAMELIST_EOF
ENDNAMELIST

# &tsteps dtts=896., dtuv=896., dtsf=16.,
cat > namelist.tsteps << ENDNAMELIST
 &tsteps dtts=86400., dtuv=3600., dtsf=3600.,
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.riglid << ENDNAMELIST
 &riglid mxscan=2000, tolrsf=1.0e8, tolrsp=1.0e-4,
         tolrfs=1.0e-4,
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.mixing << ENDNAMELIST
 &mixing am=1.e9, ah=1.e8, ambi=1.e22, ahbi=1.e21,
         kappa_m=20.0, kappa_h=1.0, aidif=1.0,
         nmix=17, eb=.true.,  ncon=1, cdbot=0.0,
         acor=0.0, dampts=50.0, 50.0, 
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.isopyc << ENDNAMELIST
 &isopyc ahisop=1.e7, slmx=0.01, athkdf=1.e7,   
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.blmix << ENDNAMELIST
 &blmix   
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.hlmix << ENDNAMELIST
 &hlmix hl_depth=500.0e2, hl_back=1.e5, hl_max=1.e9,   
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.ppmix << ENDNAMELIST
 &ppmix wndmix=10.0,  
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.diagn << ENDNAMELIST
 &diagn  tsiint=0.0, 
         tavgint=$diag,  itavg=.true.,
         tmbint=$diag,    itmb=.true., tmbper=$diag,
         trmbint=$diag,  itrmb=.true., 
         gyreint=$diag,  igyre=.true., 
	 snapint=$diag, snapls=-90.0,  snaple=90.0,   snapde=5000.0e2,
	 timavgint=$diag, timavgper=$diag,
         glenint=$diag,  vmsfint=$diag, stabint=$diag, zmbcint=$diag,
         extint=$diag,   prxzint=$diag, dspint=$diag, dspper=$diag,
         trajint=$diag,  xbtint=$diag, xbtper=$diag, exconvint=$diag, cmixint=$diag,
         crossint=$diag, densityint=$diag, tyzint=$diag,
 $NAMELIST_EOF
ENDNAMELIST

cat > namelist.io << ENDNAMELIST
 &io     iotavg=$iotavg, iotmb=$iotmb,   iotrmb=$iotrmb, iozmbc=$iozmbc,
         ioglen=$ioglen, iovmsf=$iovmsf, iogyre=$iogyre,
         ioprxz=$ioprxz, ioext=$ioext,   iodsp=$iodsp,
         iotsi=$iotsi,   iotraj=$iotraj, ioxbt=$ioxbt,
 $NAMELIST_EOF         
ENDNAMELIST

cat > namelist.ictime << ENDNAMELIST
 &ictime eqyear=.true., eqmon=.false., refinit=.true., 
         year0=1, month0=1, day0=1, hour0=0, min0=0, sec0=0,
 $NAMELIST_EOF         
ENDNAMELIST

echo " "
echo ' ==> the namelists are:'
cat namelist*

# ======================================================================
# All assigns are done from fortran. Remove file environment variable
# ======================================================================

echo '==>Assigning files...'
/bin/rm .assign
setenv FILENV .assign

# ======================================================================
# Bring in real data constructed by PREP_DATA if not running idealized
# ======================================================================


if ($LEVITUS_DATA_IC == 'yes') then
 if (!(-r temp.mom.ic) || !(-r salt.mom.ic)) then
   echo "...getting and copying to temp.mom.ic and salt.mom.ic"
   dmget $datainpath/jan.temp.mom $datainpath/jan.salt.mom
   /bin/cp $datainpath/jan.temp.mom temp.mom.ic 
   /bin/cp $datainpath/jan.salt.mom salt.mom.ic
 endif 
endif

if ($SPONGE_DATA == 'yes') then
 if (!(-r sponge.mom)) then
   echo "...getting and copying sponge.mom"
   dmget $datainpath/sponge.mom
   /bin/cp $datainpath/sponge.mom .
 endif
endif

if ($SCRIPPS_DATA == 'yes') then
 if (!(-r scripps.top)) then
   echo "...getting and copying scripps.top"
   dmget $DATABASE/scripps.top
   /bin/cp $DATABASE/scripps.top .
 endif
endif

if ($CASE == '1' || $CASE == '2') then
 if (!(-r taux.mom) || !(-r tauy.mom)) then
   echo "...getting and copying taux, tauy, SST, and SSS"
   dmget $datainpath/taux.mom $datainpath/tauy.mom $datainpath/sst.mom $datainpath/sss.mom
   /bin/cp $datainpath/taux.mom .
   /bin/cp $datainpath/tauy.mom .
   /bin/cp $datainpath/sst.mom .
   /bin/cp $datainpath/sss.mom .
 endif
endif

# ======================================================================
# Get the archive.time file to insure the restart file is correct one
# ======================================================================

 rcp "$myHOST$printoutpath/archive.nersc.$CASE" archive.time 

# ======================================================================
# Run the model
# ======================================================================

echo "==> Running with $NUM_PROCESSORS processor(s)"

a.out > results
echo "copying results to $printoutpath/printout.nersc.$CASE"
rcp results "$myHOST$printoutpath/printout.nersc.$CASE"  
echo " "
/bin/ls -laF *.dta
/bin/ls -laF *.dta.nc

# ======================================================================
# Save the archive.time file to verify next restart file is correct one
# ======================================================================

rcp archive.time "$myHOST$printoutpath/archive.nersc.$CASE"  

echo " "
echo "==> done"
